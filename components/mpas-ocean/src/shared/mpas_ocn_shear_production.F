! Copyright (c) 2013,  Los Alamos National Security, LLC (LANS)
! and the University Corporation for Atmospheric Research (UCAR).
!
! Unless noted otherwise source code is licensed under the BSD license.
! Additional copyright and license information can be found in the LICENSE file
! distributed with this code, or at http://mpas-dev.github.com/license.html
!
!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
!
!  ocn_shear_production
!
!> \brief MPAS two equation turbulence model shear production
!> \author Luke Van Roekel 
!> \date   June 2021
!> \details
!>  This module contains the routines for computing shear production term 
!>  for the two equation turbulence closure
!>
!
!-----------------------------------------------------------------------

module ocn_shear_production

   use mpas_timer
   use mpas_derived_types
   use mpas_pool_routines
   use mpas_threading

   use ocn_constants
   use ocn_diagnostics_variables
   use ocn_config
   use ocn_mesh

   implicit none
   private
   save

   !--------------------------------------------------------------------
   !
   ! Public parameters
   !
   !--------------------------------------------------------------------

   !--------------------------------------------------------------------
   !
   ! Public member functions
   !
   !--------------------------------------------------------------------

   public :: ocn_shear_production_compute

   !--------------------------------------------------------------------
   !
   ! Private module variables
   !
   !--------------------------------------------------------------------


!***********************************************************************

contains

!***********************************************************************
!
!  routine ocn_shear_production_compute
!
!> \brief   Computes shear production term for the two equation model 
!> \author  Luke Van Roekel
!> \date    July 2021
!> \details
!>  This routine computes the shear production term for the  
!>  two equation model.
!
!-----------------------------------------------------------------------

   subroutine ocn_shear_production_compute(tkeTend, glsPsiTend, tke, glsPsi, &
                  c_psi1, surfaceFrictionVelocity, c_mu, psiOverK, &
                  turbLength, smagorinsky_coefficient, err)
      !-----------------------------------------------------------------
      !
      ! input/output variables
      !
      !-----------------------------------------------------------------

      real (kind=RKIND), dimension(:,:), intent(inout) :: &
         tkeTend,glsPsiTend          !< Input/Output:  tendencies

      real (kind=RKIND), dimension(:,:), intent(in) :: &
         tke, glsPsi, psiOverK, turbLength          !< Input: tke and psi variable at current timestep 

      real (kind=RKIND), dimension(:), intent(in) :: &
         surfaceFrictionVelocity 

      real (kind=RKIND), intent(in) :: c_psi1, c_mu, smagorinsky_coefficient

      !-----------------------------------------------------------------
      !
      ! output variables
      !
      !-----------------------------------------------------------------

      integer, intent(out) :: err !< Output: error flag

      !-----------------------------------------------------------------
      !
      ! local variables
      !
      !-----------------------------------------------------------------

      integer :: iCell, iEdge, cell1, cell2
      integer :: i, k, nCells

      real (kind=RKIND) :: invAreaCell
      real (kind=RKIND) :: viscMid
      !real (kind=RKIND) :: shearProduction, viscMid

      err = 0

      call mpas_timer_start("shear production compute")

      !$omp parallel
      !$omp do schedule(runtime) &
      !$omp private(k, c_psi2, viscMid, shearProduction)
      do iCell = 1, nCellsOwned
         k = 1
         viscMid = vertViscTopOfCell(k+1,iCell) !assume visc equal across top cell
         viscMid = min(1e-6_RKIND, viscMid)
         shearProduction(k,iCell) = 2.0_RKIND*(viscMid*ugrad(1,k,iCell) - tke(k,iCell)/3.0_RKIND)* &
                                    ugrad(1,k,iCell) + viscMid*(ugrad(2,k,iCell) + vgrad(1,k,iCell))**2 &
                                    + viscMid*(ugrad(3,k,iCell) + wgrad(1,k,iCell))**2 &
                                     + 2.0_RKIND*(viscMid*vgrad(2,k,iCell) -          &
                                    tke(k,iCell)/3.0_RKIND)*vgrad(2,k,iCell) + viscMid*(vgrad(3,k,iCell) + &
                                    wgrad(2,k,iCell))**2 + 2.0_RKIND*(viscMid*wgrad(3,k,iCell) - &
                                    tke(k,iCell)/3.0_RKIND)*wgrad(3,k,iCell) !+ &!FSP - surfaceFrictionVelocity to be added

         tkeTend(k,iCell) = tkeTend(k,iCell) + max(0.0_RKIND,shearProduction(k,iCell))
         glsPsiTend(k,iCell) = glsPsiTend(k,iCell) + psiOverK(k,iCell) &
                               *c_psi1*max(0.0_RKIND,shearProduction(k,iCell))


         do k=2,maxLevelCell(iCell)
            viscMid = 0.5_RKIND*(vertViscTopOfCell(k,iCell) + vertViscTopOfCell(k+1,iCell))
            viscMid = min(1e-6_RKIND, viscMid)
!            viscMid = smagorinsky_coefficient*turbLength(k,iCell)*sqrt(tke(k,iCell))
            shearProduction(k,iCell) = 2.0_RKIND*(viscMid*ugrad(1,k,iCell) - tke(k,iCell)/3.0_RKIND)* &
                                    ugrad(1,k,iCell) + viscMid*(ugrad(2,k,iCell) + vgrad(1,k,iCell))**2 &
                                    + viscMid*(ugrad(3,k,iCell) + wgrad(1,k,iCell))**2 &
                                     + 2.0_RKIND*(viscMid*vgrad(2,k,iCell) -          &
                                    tke(k,iCell)/3.0_RKIND)*vgrad(2,k,iCell) + viscMid*(vgrad(3,k,iCell) + &
                                    wgrad(2,k,iCell))**2 + 2.0_RKIND*(viscMid*wgrad(3,k,iCell) - &
                                    tke(k,iCell)/3.0_RKIND)*wgrad(3,k,iCell) !+ &!FSP - surfaceFrictionVelocity to be added

            tkeTend(k,iCell) = tkeTend(k,iCell) + max(0.0_RKIND,shearProduction(k,iCell))
            glsPsiTend(k,iCell) = glsPsiTend(k,iCell) + psiOverK(k,iCell) &
                                  *c_psi1*max(0.0_RKIND,shearProduction(k,iCell))
         end do 
         !TODO - need a bottom drag term here.
      end do
      !$omp end do
      !$omp end parallel

      call mpas_timer_stop("shear production compute")

   !--------------------------------------------------------------------

   end subroutine ocn_shear_production_compute

end module ocn_shear_production

!|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||
! vim: foldmethod=marker
